plugins {
    id 'org.sonarqube' version '5.0.0.4638'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }

    group = 'ru.rapidcoder.trader'
    version = '0.0.1-SNAPSHOT'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    dependencies {
        implementation("org.slf4j:slf4j-api:2.+")
        implementation("ch.qos.logback:logback-core:1.5.+")
        implementation("ch.qos.logback:logback-classic:1.5.+")

        implementation("ru.tinkoff.piapi:java-sdk-core:1.27")

        implementation ("org.xerial:sqlite-jdbc:3.51.1.0")
        implementation ("org.flywaydb:flyway-core:11.18.0")
        implementation ("com.zaxxer:HikariCP:7.0.2")
        implementation ("org.hibernate.orm:hibernate-core:7.1.11.Final")
        implementation ("org.hibernate.orm:hibernate-community-dialects:7.1.11.Final")

        testImplementation("junit:junit:4.13.2")
        testImplementation("org.junit.jupiter:junit-jupiter:5.9.3")
        testImplementation("org.mockito:mockito-core:5.8.0")
        testImplementation("org.mockito:mockito-junit-jupiter:5.8.0")
        testImplementation("org.assertj:assertj-core:3.24.2")
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
            showStandardStreams = true
        }

        minHeapSize = "512m"
        maxHeapSize = "1024m"

        finalizedBy jacocoTestReport
    }

    jacoco {
        toolVersion = "0.8.10"
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.1 // 10% coverage
                }
            }
        }
    }

    jacocoTestReport {
        reports {
            xml.required = true
            html.required = true

            html.outputLocation = layout.buildDirectory.dir("${rootDir}/reports/jacoco/html")

            afterEvaluate {
                classDirectories.setFrom(files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: [
                            '**/config/**'
                    ])
                }))
            }
        }
    }

    sonar {
        properties {
            property 'sonar.gradle.skipCompile', 'true'
            property 'sonar.host.url', 'https://sonarcloud.io'
            property 'sonar.organization', System.getenv('SONAR_ORGANIZATION')
            property 'sonar.projectKey', System.getenv('SONAR_PROJECT_KEY')

            property 'sonar.sourceEncoding', 'UTF-8'
            property 'sonar.java.source', '17'
            property 'sonar.java.binaries', 'build/classes/java/main'
            property 'sonar.java.libraries', 'build/libs/*.jar'
            property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
        }
    }
}